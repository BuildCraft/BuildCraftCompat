// DON'T TOUCH THE BUILDSCRIPT[] BLOCK
// its special, and it is only there to make ForgeGradle work correctly.

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven/"
    }
    maven {
        url = "http://maven.tterrag.com"
    }
    maven {
        url = "http://maven.ic2.player.to"
    }
    maven {
        url = "http://mobiusstrip.eu/maven"
    }
    maven {
        name = "Qmunity"
        url = "http://maven.bluepowermod.com/"
    }
    ivy {
        name "MineTweaker3"
        artifactPattern "http://minetweaker3.powerofbytes.com/download/[module]-[classifier]-[revision].[ext]"
    }
    ivy {
        name "BuildCraft"
        artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision]-[classifier].[ext]"
    }
}

apply plugin: 'forge' // adds the forge dependency
apply plugin: 'maven' // for uploading to a maven repo
// apply plugin: 'checkstyle'

version = "7.0.3"
group = "com.mod-buildcraft-compat"
archivesBaseName = "buildcraft-compat" // the name that all artifacts will use as a base. artifacts names follow this pattern: [baseName]-[appendix]-[version]-[classifier].[extension]

dependencies {
    compile "codechicken:CodeChickenLib:1.7.10-1.1.3.136:dev"
    compile "codechicken:CodeChickenCore:1.7.10-1.0.6.43:dev"
    compile "codechicken:NotEnoughItems:1.7.10-1.0.4.106:dev"
    compile "codechicken:ForgeMultipart:1.7.10-1.1.1.320:dev"
    compile("net.sengir.forestry:forestry_1.7.10:3.4.0.7:dev") {
        exclude module: "buildcraft"
    }
    compile "qmunity:QmunityLib:0.1.109:deobf"
    compile("com.enderio:EnderIO:1.7.10-2.2.6.324:dev") {
        exclude module: "buildcraft"
    }
    compile name: "MineTweaker3", version: "1.7.10-3.0.9C", classifier: "Dev"
    compile ("appeng:appliedenergistics2:rv2-beta-8:dev") {
        transitive = false
    }
    compile name: "buildcraft", version: "7.0.5", classifier: "dev"
}

minecraft {
    version = "1.7.10-10.13.3.1388-1.7.10" // McVersion-ForgeVersion     this variable is later changed to contain only the MC version, while the apiVersion variable is used for the forge version.  Yeah its stupid, and will be changed eentually.

    runDir = "run" // the directory for ForgeGradle to run Minecraft in

    // replacing stuff in the source
    replace '@VERSION@', project.version
    replace '@MC_VERSION@', version
}

// configure  the source folders
sourceSets {
    main {
        java {
            srcDir 'common'
        }
        resources {
            srcDir 'resources'
        }
    }
    api {
        java {
            srcDir 'api'
        }
    }
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        // ${version}   and    ${mcversion}    are the exact strings being replaced
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, that's not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// --------------------
// extra jar section
// -------------------

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

// checkstyle {
//     sourceSets = [ sourceSets.main ]
//     configFile = file('guidelines/buildcraft.checkstyle')
// }


artifacts {
    archives sourceJar
    archives deobfJar
}
